def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply plugin: 'com.android.application'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

// 配置打包签名文件
def configProperties = new Properties()
def configPropertiesFile = rootProject.file('config.properties')
if (configPropertiesFile.exists()) {
    configProperties.load(new FileInputStream(configPropertiesFile))
}

android {
    compileSdkVersion 28

    lintOptions {
        disable 'InvalidPackage'
        checkReleaseBuilds false
        abortOnError false
    }

    defaultConfig {
        applicationId "com.jxtele.pollution_source"
        // 为了使用图片选择器插件 minSdkVersion改成19
        minSdkVersion 19
        targetSdkVersion 28
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    // 签名配置
    signingConfigs {
        release {
            keyAlias configProperties['keyAlias']
            keyPassword configProperties['keyPassword']
            storePassword configProperties['storePassword']
            storeFile file(configProperties['storeFile'])
        }
    }

    buildTypes {
        debug {
            manifestPlaceholders = [APP_NAME     : configProperties['applicationNameDebug'],
                                    JPUSH_PKGNAME: configProperties['applicationPackageNameDebug'],
                                    JPUSH_APPKEY : configProperties['jpushKeyDebug'],
                                    JPUSH_CHANNEL: "developer-default",
                                    BAIDU_KEY    : configProperties['baiduKeyDebug'],
            ]
            applicationIdSuffix configProperties['applicationIdSuffixDebug']
            ndk {
                abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            }
        }
        release {
            manifestPlaceholders = [APP_NAME     : configProperties['applicationName'],
                                    JPUSH_PKGNAME: configProperties['applicationPackageName'],
                                    JPUSH_APPKEY : configProperties['jpushKey'],
                                    JPUSH_CHANNEL: "developer-default",
                                    BAIDU_KEY    : configProperties['baiduKey'],
            ]
            applicationIdSuffix configProperties['applicationIdSuffix']
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ndk {
                abiFilters 'armeabi', 'armeabi-v7a'
            }
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}
